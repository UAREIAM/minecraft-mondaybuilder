plugins {
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

version = "0.0.3"
base {
    archivesName = "mondaybuilder-fabric"
}

println "Fabric project version: ${project.version}"

// Add this to ensure common resources are included properly in the development environment
dependencies {
    // ... existing dependencies
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')
}

processResources {
    def modVersion = project.version.toString()
    inputs.property "version", modVersion

    filesMatching("fabric.mod.json") {
        expand "version": modVersion
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
    archiveVersion = rootProject.mod_version
    preserveFileTimestamps = true
    reproducibleFileOrder = false
}

remapJar {
    inputFile.set shadowJar.archiveFile
    archiveVersion = rootProject.mod_version
    preserveFileTimestamps = true
    reproducibleFileOrder = false
}

loom {
    runs {
        server {
            runDir "run"
            programArgs "--world", "mondaybuilder"
        }
    }
}

tasks.register("updateResourcePack", Zip) {
    from project(":common").sourceSets.main.resources
    include "assets/mondaybuilder/**"
    include "pack.mcmeta"
    exclude "**/*.flp"
    exclude "**/Mondaybuilder/**"
    archiveFileName = "mondaybuilder.zip"
    destinationDirectory = file("run/resourcepacks")
}

tasks.named("runClient") {
    dependsOn "updateResourcePack"
}
